apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = PUBLISH_DESCRIPTION
                    url = 'https://github.com/corca-ai/adcio_android_plugins'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/corca-ai/adcio_android_plugins/blob/develop/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = "corca-ai"
                            name = "corca-ai"
                            email = "contact@corca.ai"
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/corca-ai/adcio_android_plugins.git'
                        developerConnection = 'scm:git:ssh://github.com/corca-ai/adcio_android_plugins.git'
                        url = PUBLISH_SCM_URL
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}